@PACKAGE_INIT@

# get a config name
if(@PROJECT_NAME@_STATIC_LIBRARIES)
    set(config_name Static)
else()
    set(config_name Shared)
endif()

# include an export targets file
set(export_targets_file "${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@${config_name}Targets.cmake")
if(NOT EXISTS "${export_targets_file}")
    set(@PROJECT_NAME@_FOUND FALSE)
    set(@PROJECT_NAME@_NOT_FOUND_MESSAGE "Requested configuration ${export_targets_file} was not found")
else()
    include("${export_targets_file}")  
endif()

# find components
foreach(component ${@PROJECT_NAME@_FIND_COMPONENTS})

    if(NOT TARGET ${component})
        if(@PROJECT_NAME@_FIND_REQUIRED_${component})
            set(@PROJECT_NAME@_FOUND FALSE)
            set(@PROJECT_NAME@_NOT_FOUND_MESSAGE "Found @PROJECT_NAME@ but requested component '${component}'\
                                          is missing in the config defined in ${@PROJECT_NAME@_DIR}.")
        endif()
    endif()

endforeach()